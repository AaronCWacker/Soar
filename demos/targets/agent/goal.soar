sp {init-unachieved-goal-predicates
   (state <s> ^task-state t)
-->
   (<s> ^unachieved-goals <g>)
   (<g> ^positive <p> ^negative <n>)}

sp {mark-unachieved-goal-predicate*2-params
   (state <s> ^goal-state.<sign> <gs>
              ^curr-state.<sign> <cs>
              ^unachieved-goals.<sign> <up>)
   (<gs> ^<pred> <gp>)
   (<gp> ^<param1> <val1>
         ^{<param2> > <param1>} <val2>
        -^{<param3> > <param2> > <param1>})
 -{(<cs> ^<pred> <cp>)
   (<cp> ^<param1> <val1> ^<param2> <val2>)}
-->
   (<up> ^<pred> <gp>)}

# goal predicates are always important, so always extract these
sp {extract-goal-preds*2
   (state <s> ^svs.command <cmd>
              ^goal-state.<pos-or-neg> <gs>)
   (<gs> ^<pred> <p>)
   (<p> ^<param1> <val1>
        ^{<param2> > <param1>} <val2>
       -^{<param3> > <param2> > <param1>})
-->
   (<cmd> ^extract <e>)
   (<e> ^type <pred> ^<param1> <p1> ^<param2> <p2>)
   (<p1> ^type node ^name <val1>)
   (<p2> ^type node ^name <val2>)}

sp {prefer*goal-preds*2-params
   (state <s> ^unachieved-goals.<sign>.<pred> <p1>
              ^operator <o> +)
   (<p1> ^<param1> <v1>
         ^{<param2> > <param1>} <v2>)
  -(<p1> ^{<param3> > <param2>} <v3>)
   (<o> ^name change-predicate
        ^sign <sign>
        ^predicate <pred>
        ^params <p2>)
   (<p2> ^<param1> <v1>
         ^<param2> <v2>)
-->
   (<s> ^operator <o> >)}
