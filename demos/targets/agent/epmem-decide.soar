
sp {init*epmem-decide
   (state <s> ^impasse tie
              ^attribute operator
              ^superstate.task-state t)
-->
   (<s> ^name epmem-decide)}

sp {epmem-decide*retrieve-first
   (state <s> ^name epmem-decide
              ^superstate.curr-state <cs>
             -^first
              ^epmem.command <c>)
-->
   (<c> ^query.curr-state <cs>)}

sp {epmem-decide*propose*copy-first
   (state <s> ^name epmem-decide
             -^first
              ^epmem.result.retrieved <r>)
-->
   (<s> ^operator.name copy-first)}

sp {epmem-decide*apply*copy-first
   (state <s> ^name epmem-decide
              ^operator.name copy-first
              ^epmem.result.retrieved <r>)
-->
   (<s> ^first <r>)}

sp {epmem-decide*detect-change*2
   (state <s> ^name epmem-decide
              ^superstate <ss>
              ^first.curr-state.<sign> <cs1>
              ^epmem.result.retrieved.curr-state.<sign> <cs2>)
   (<cs1> ^<pred> <p1>)
   (<p1> ^<param1> <v1>
         ^{<param2> > <param1>} <v2>
        -^{<param3> > <param2>})

 -{(<cs2> ^<pred> <p2>)
   (<p2> ^<param1> <v1> ^<param2> <v2>)}
   
   (<ss> ^operator <o> +)
   (<o> ^name change-predicate
        ^predicate <pred>
        ^sign <sign>
        ^params <p3>)
   (<p3> ^<param1> <v1>
         ^<param2> <v2>)
-->
   (<s> ^change <c>)
   (<c> ^sign <sign>
        ^predicate <pred>
        ^params <p1>)}

sp {epmem-decide*propose*retrieve-next*1
   (state <s> ^name epmem-decide
              ^first <f>
             -^change
              ^epmem.command <cmd>)
   (<cmd> -^next)
-->
   (<s> ^operator <o> +)
   (<o> ^name get-next)}

sp {epmem-decide*propose*get-next*2
   (state <s> ^name epmem-decide
              ^first <f>
              ^epmem <epmem>
             -^change)
   (<epmem> ^result.memory-id <mid>
            ^command.next <n>)
-->
   (<s> ^operator <o> +)
   (<o> ^name get-next
        ^remove <n>)}

sp {epmem-decide*apply*get-next
   (state <s> ^name epmem-decide
              ^operator <o>
              ^epmem.command <cmd>)
   (<o> ^name get-next)
-->
   (<cmd> ^next <n1>)}
   
sp {epmem-decide*apply*get-next*remove-prev
   (state <s> ^name epmem-decide
              ^operator <o>
              ^epmem.command <cmd>)
   (<o> ^name get-next
        ^remove <n>)
   (<cmd> ^next <n>)
-->
   (<cmd> ^next <n> -)}

sp {epmem-decide*choose*2
   (state <s> ^name epmem-decide
              ^superstate <ss>
              ^change <c>)
   (<c> ^predicate <pred>
        ^sign <sign>
        ^params <p1>)
   (<p1> ^<param1> <v1>
         ^{<param2> > <param1>} <v2>
        -^{<param3> > <param2>})
   (<ss> ^operator <o> +)
   (<o> ^name change-predicate
        ^predicate <pred>
        ^sign <sign>
        ^params <p2>)
   (<p2> ^<param1> <v1>
         ^<param2> <v2>)
-->
   (<ss> ^operator <o> >)}
