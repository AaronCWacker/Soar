import sys

Import('env', 'compiler')

# svs viewer
viewer_src = [ 'viewer/%s.c' % f for f in ('input', 'text', 'trackball', 'viewer', 'util') ]
viewer_env = env.Clone()
viewer_env['LIBS'] = []

if compiler == 'msvc':
	viewer_libs = [ 'SDL', 'SDLmain', 'opengl32', 'glu32' ]
	viewer_env.Append(
		CPPFLAGS  = [ '/D', 'WIN32', '/MD'],
		CPPPATH   = [ 'SDL/include' ],
		LIBPATH   = [ 'SDL/lib/x64' ],
		LIBS      = [ 'Ws2_32', 'Mswsock', 'AdvApi32' ],
		LINKFLAGS = [ '/SUBSYSTEM:CONSOLE' ]
	)
	viewer_src.append('viewer/windows.c')
else:
	viewer_libs = [ 'SDL', 'GL', 'GLU', 'm' ]
	viewer_src.append('viewer/linux.c')

config = Configure(viewer_env)
missing_libs = [ l for l in viewer_libs if not config.CheckLib(l)]
viewer_env = config.Finish()
if not missing_libs:
	viewer_prog = viewer_env.Program('svs_viewer', viewer_src)
	viewer_install = viewer_env.Alias('svs_viewer', viewer_env.Install('$OUT_DIR', viewer_prog))
else:
	print 'Cannot find %s, not building svs_viewer' % ', '.join(missing_libs)

# svs_test
test_prog = env.Program('test_svs', 'src/progs/test_svs.cpp')
test_install = env.Alias('test_svs', env.Install('$OUT_DIR', test_prog))

# svs library objects
svs_env = env.Clone()
svs_env['LIBS'] = []

srcdirs = ['src', 'src/filters', 'src/commands', 'src/models', 'src/algorithms']
incdirs = [env.Dir(d).srcnode() for d in 'src src/algorithms src/models eigen ccd'.split()]

if compiler == 'g++':
	flags = [
		# By default Eigen will try to align all fixed size vectors to 128-bit
		# boundaries to enable SIMD instructions on hardware such as SSE. However,
		# this requires that you modify every class that has such vectors as members
		# so that they are correctly allocated. This seems like more trouble than
		# it's worth at the moment, so I'm disabling it.
		'-DEIGEN_DONT_ALIGN',
		'-Wno-enum-compare',
	]
	srcdirs.append('src/posix')
	incdirs.append('src/posix')
elif compiler == 'msvc':
	flags = [
		'/D', 'EIGEN_DONT_ALIGN',
		'/wd4307',  # disables warning C4307 that's generated by Eigen code
	]
	srcdirs.append('src/windows')
	incdirs.append('src/windows')

src = []
for d in srcdirs:
	src += Glob(d + '/*.cpp')

svs_env.Prepend(
	CPPPATH = incdirs,
	CPPFLAGS = flags
)

ccd_env = env.Clone()
ccd_env['LIBS'] = []
ccd_env['CPPPATH'] = [env.Dir('ccd').srcnode()]
ccd_src = Glob('ccd/*.c')

if compiler == 'msvc':
	ccd_env.Append(CPPFLAGS=['/D', 'WIN32'])
	
if GetOption('static'):
	svs_objs = svs_env.Object(src) + ccd_env.Object(ccd_src)
else:
	svs_objs = svs_env.SharedObject(src) + ccd_env.SharedObject(ccd_src)

svs_inc = ['SVS/src']
Return('svs_objs', 'svs_inc')
