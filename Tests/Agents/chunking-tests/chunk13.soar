# Test where RHS item should not be variablized and is never variablized on LHS (like example 14 with identifiers)
# RHS should still be a variable b/c it becomes an unground one
# was 16

srand 23
learn --on

sp {init-superstate
    (state <s> ^superstate nil)
-->
    (<s> ^test-item 9 7 6
         ^test-item2 9 7
         ^test-param <tp> <tp2> <tp3>
         ^test-param2 <tp3>
         ^test-current 1
         ^bar blue
         ^test-id <ti>)
    (<tp3> ^foo <tp> ^foo <tp2>)
    (<tp> ^value 9 8)
    (<tp2> ^value <dummy1> <dummy2>)
    (write (crlf) |Starting chunking unit test.|)
}

sp {propose*top
    (state <s> ^superstate nil)
-->
    (<s> ^operator <o>)
    (<o> ^name do-test)
}

sp {apply*test-done
    (state <s> ^superstate nil ^test-done)
-->
    (write (crlf) |Chunk produced. Test completed.|)
    (exec succeeded)
    (halt)
}

sp {apply*test-failed
    (state <s> ^superstate.superstate.superstate.superstate nil)
-->
    (write (crlf) |Chunk not produced.  Test failed.|)
    (halt)
}

sp {init-substate
    (state <s> ^superstate.superstate nil)
-->
    (<s> ^name substate)}

sp {propose*test
    (state <s> ^name substate)
-->
    (<s> ^operator <o> +)
    (<o> ^name test)}

sp {apply13a
    (state <s> ^name substate
               ^operator.name test
               ^superstate <ss>)
-->
    (<s> ^substate-output <ungrounded>)}

sp {apply13b
    (state <s> ^name substate
               ^operator.name test
               ^substate-output <var-ungrounded>
               ^superstate <ss>)
-->
    (<ss> ^result13a <var-ungrounded>
          ^test-done 1)}

#run
#ctf expected/chunk13.soar print -cf